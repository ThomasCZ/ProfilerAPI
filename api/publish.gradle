apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'cz.chladek'
version = '1.0.0'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/api-release.aar")
            artifact sourcesJar
            artifact javadocJar

            groupId this.group
            artifactId 'profiler-api'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    override = true
    pkg {
        repo = 'maven'
        name = 'profiler-api'
        description = "API for Android application Profiler"
        labels = ['android', 'profiler', 'api']
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ThomasCZ/ProfilerAPI.git'
        dryRun = false
        version {
            name = this.version
            desc = "v${this.version}"
            released = new Date()
            vcsTag = "https://github.com/ThomasCZ/ProfilerAPI/releases/tag/v${this.version}"
        }
    }
}